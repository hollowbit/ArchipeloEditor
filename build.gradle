apply plugin: "java"
apply plugin: 'eclipse'

//mainClassName = 'net.hollowbit.archipeloeditor.MainEditor'
sourceSets.main.java.srcDirs = [ "src/main/java", "../ArchipeloClient/src-shared/" ]
sourceSets.main.resources.srcDirs = [ "src/main/resources/" ]
project.ext.assetsDir = new File("../ArchipeloClient/core/assets");
project.ext.mainClassName = 'net.hollowbit.archipeloeditor.MainEditor'

sourceCompatibility = 1.8
version = '1.0'

jar {
    manifest {
        attributes 'Implementation-Title': 'Gradle Quickstart',
                   'Implementation-Version': version
    }
}

task run(dependsOn: classes, type: JavaExec) {
    main = project.mainClassName
    classpath = sourceSets.main.runtimeClasspath
    classpath +=sourceSets.main.resources
    standardInput = System.in
    //workingDir = project.assetsDir
    ignoreExitValue = true
    doFirst {
        println("Assets: $project.assetsDir.absolutePath")
    }
}

repositories {
    mavenCentral()
}

eclipse {
	project {
        linkedResource name: 'assets', type: '2', location: 'PARENT-1-PROJECT_LOC/ArchipeloClient/core/assets/'
        linkedResource name: 'src-shared', type: '2', location: 'PARENT-1-PROJECT_LOC/ArchipeloClient/src-shared/'
    }
}

test {
    systemProperties 'property': 'value'
}

dependencies {
        compile "com.badlogicgames.gdx:gdx-backend-lwjgl:1.9.4"
        compile "com.badlogicgames.gdx:gdx-platform:1.9.4:natives-desktop"
        compile "com.badlogicgames.gdx:gdx:1.9.4"
}

uploadArchives {
    repositories {
       flatDir {
           dirs 'repos'
       }
    }
}

task afterEclipseImport(description: "Post processing after project generation", group: "IDE") {
  doLast {
    def classpath = new XmlParser().parse(file(".classpath"))
    new Node(classpath, "classpathentry", [ kind: 'src', path: 'src-shared' ]);
    new Node(classpath, "classpathentry", [ kind: 'src', path: 'assets' ]);
    def writer = new FileWriter(file(".classpath"))
    def printer = new XmlNodePrinter(new PrintWriter(writer))
    printer.setPreserveWhitespace(true)
    printer.print(classpath)
  }
}
